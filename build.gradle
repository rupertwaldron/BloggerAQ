plugins {
    id 'java'
    id "io.freefair.lombok" version "8.3"
    id 'org.springframework.boot' version '3.1.3'
    id 'io.spring.dependency-management' version '1.1.3'
}

group = 'com.ruppyrup'
version = '1.0-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'

    testImplementation "io.cucumber:cucumber-java:${cucumberVersion}"
    testImplementation "io.cucumber:cucumber-spring:${cucumberVersion}"
    testImplementation "io.cucumber:cucumber-jvm:${cucumberVersion}"
    testImplementation "io.cucumber:cucumber-junit-platform-engine:${cucumberVersion}" // this will use junit 5 cucumber-junit uses Junit4

    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.junit.platform:junit-platform-suite'
    testImplementation 'org.assertj:assertj-core:3.23.1'
}

test { // this is the basic test
    useJUnitPlatform() // using JUnit 5
}

tasks.register("runAllCukesTest", Test) {// Similar to above to shows how to add parameters if you aren't using the application.yml
    systemProperty "jwtToken", "99"
    systemProperty "dbPassword", "chimp"
    useJUnitPlatform()
    configure { // This will be this task into the verification group in the Gradle tasks
        group = 'verification'
    }
}

tasks.register("runTranslationCukesTest", Test) {
    include "**/com/ruppyrup/qa/runners/translationrunner/*" // includes this directory
    useJUnitPlatform()
    configure {
        group = 'verification'
    }
}

tasks.register("runSpringBeanCukesTest", Test) {
    exclude "**/com/ruppyrup/qa/runners/translationrunner/*" // excludes this directory
    useJUnitPlatform()
    configure {
        group = 'verification'
    }
}

wrapper {
    gradleVersion '8.3'
    distributionType Wrapper.DistributionType.ALL
}

check.dependsOn("runAllCukesTest")